#!/usr/bin/env fennel

(local http_request (require :http.request))
(local cjson (require :cjson))
(local view (require :fennel.view))


(fn slurp [path]
  (with-open [buf (io.open path)]
    (buf:read :*all)))

(fn nix-prefetch-url [url]
  (let [pipe (io.popen (string.format "nix-prefetch-url --type sha256 %s 2>/dev/null" url))
        sha256 (string.gsub (pipe:read :*all) "\n$" "")]
    (pipe:close)
    sha256))


(local http {})

(fn http.get [uri headers]
  (let [request (http_request.new_from_uri uri)]
    (each [key val (pairs (or headers {}))]
      (request.headers:append key val))
    (let [(headers stream) (assert (: request :go))
          body (assert (stream:get_body_as_string))]
     (if (= (headers:get ":status") :200)
         (values body
                 headers)
         (error (view {: body
                       : headers})))))) 


(local github {})

(fn github.get [query token]
  (let [token (or token (os.getenv "GITHUB_TOKEN"))]
    (if (not token)
        (error "Please set GITHUB_TOKEN for your github access authentication")
        (let [(body headers) (http.get (string.format "https://api.github.com/%s" query)
                                       {:authorization (string.format "token %s" token)
                                        :content-type "application/json"})]
          (values (cjson.decode body)
                  headers)))))


(set github.repo {})

(set github.repo.__index github.repo)

(fn github.repo.new [owner repo]
  (let [obj {: owner : repo}]
    (setmetatable obj github.repo)))

(fn github.repo.get_default_branch [self]
  (let [(body headers) (github.get (string.format "repos/%s/%s" self.owner self.repo))]
    body.default_branch)) 

(fn github.repo.get_recent_commit [self branch]
  (let [branch (or branch
                   (self:get_default_branch))
        body (github.get (string.format "repos/%s/%s/commits/%s" self.owner self.repo branch))]
    {: branch
     :date body.commit.committer.date
     :sha body.sha}))


(local plugin {})

(set plugin.__index plugin)

(fn plugin.new [spec]
  (match (type spec)
    :string (let [(owner repo) (string.match spec "^([^/]+)/([^/]+)$")
                  recent_commit (: (github.repo.new owner repo) :get_recent_commit)
                  attr_name (string.gsub repo "%." "-")
                  pname attr_name
                  version (string.match recent_commit.date "^%d%d%d%d%-%d%d%-%d%d")
                  rev recent_commit.sha
                  url (string.format "https://github.com/%s/%s/archive/%s.tar.gz"
                                     owner
                                     repo
                                     recent_commit.sha)
                  obj {: attr_name 
                       : pname
                       : version 
                       : owner
                       : repo
                       : rev
                       : url}]
              (set obj.sha256 (nix-prefetch-url obj.url))
              (setmetatable obj plugin))
    _ (error "plugin spec undefined")))

(fn plugin.to_nixexpr [self]
  (string.format
    "  %s = buildVimPlugin {
    pname = \"%s\";
    version = \"%s\";
    src = fetchurl {
      url = \"%s\";
      sha256 = \"%s\";
    };
  };
"
    self.attr_name
    self.pname
    self.version
    self.url
    self.sha256))

(fn plugin.to_markdown_bullet [self]
  (string.format
    "- [%s/%s](https://github.com/%s/%s) (last change: %s, package name: `%s`)\n"
    self.owner
    self.repo
    self.owner
    self.repo
    self.version
    self.attr_name))

(fn plugin.to_markdown_table_row [self]
  (string.format
    "| [%s/%s](https://github.com/%s/%s) | %s | `%s` |\n"
    self.owner
    self.repo
    self.owner
    self.repo
    self.version
    self.attr_name))


(local specs (cjson.decode (slurp "vim-plugins.json")))


(fn main []
  (with-open [nix (io.open "pkgs/vim-plugins.nix" :w)
              readme (io.open "README.md" :w)]
    (nix:write "{ buildVimPlugin, fetchurl }:\n\n{\n")
    (readme:write (slurp "templates/README_header.md"))
    (each [_ spec (ipairs specs)]
      (let [pkg (plugin.new spec)]
        (nix:write (pkg:to_nixexpr))
        (readme:write (pkg:to_markdown_table_row))))
    (nix:write "}\n")
    (readme:write (slurp "templates/README_footer.md"))))

(main)
