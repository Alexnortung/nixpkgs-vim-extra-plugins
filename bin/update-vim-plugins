#!/usr/bin/env fennel

(local http_request (require :http.request))
(local cjson (require :cjson))
(local view (require :fennel.view))

(fn basename [path]
  (string.gsub path "(.*/)(.*)" "%2"))

(fn slurp [path]
  (with-open [buf (io.open path)]
    (buf:read :*all)))


(local nix {})

(fn nix.license_from_github_license_key [key]
  (let [map {:agpl-3.0 "agpl3Only"
             :apache-2.0 "asl20"
             :bsd-2-clause "bsd2"
             :bsd-3-clause "bsd3"
             :bsl-1.0 "boost"
             :cc0-1.0 "cc0"
             :epl-2.0 "epl20"
             :gpl-2.0 "gpl2Only"
             :gpl-3.0 "gpl3Only"
             :lgpl-2.1 "lgpl21Only"
             :mit "mit"
             :mpl-2.0 "mpl20"
             :unlicense "unlicense"}]
    (. map key)))

(fn nix.prefetch-url [url]
  (let [pipe (io.popen (string.format "nix-prefetch-url --type sha256 %s 2>/dev/null" url))
        sha256 (string.gsub (pipe:read :*all) "\n$" "")]
    (pipe:close)
    sha256))


(local http {})

(fn http.get [uri headers]
  (let [request (http_request.new_from_uri uri)]
    (each [key val (pairs (or headers {}))]
      (request.headers:append key val))
    (let [(headers stream) (assert (: request :go))
          body (assert (stream:get_body_as_string))]
     (if (= (headers:get ":status") :200)
         (values body
                 headers)
         (error (view {: body
                       : headers})))))) 


(local github {})

(fn github.get [query token]
  (let [token (or token (os.getenv "GITHUB_TOKEN"))]
    (if (not token)
        (error "Please set GITHUB_TOKEN for your github access authentication")
        (let [(body headers) (http.get (string.format "https://api.github.com/%s" query)
                                       {:authorization (string.format "token %s" token)
                                        :content-type "application/json"})]
          (values (cjson.decode body)
                  headers)))))


(set github.repo {})

(set github.repo.__index github.repo)

(fn github.repo.new [full_name_or_owner name]
  (let [info (let [full_name (if (not name)
                                 full_name_or_owner
                                 (string.format "%s/%s" full_name_or_owner name))]
               (github.get (string.format "repos/%s" full_name)))
        obj {:full_name info.full_name
             :owner (if (not name)
                        (pick-values 1 (string.match info.full_name "^([^/]+)/([^/]+)$"))
                        full_name_or_owner)
             :name info.name
             :default_branch info.default_branch
             :description info.description
             :is_fork info.fork
             :homepage (if (or (= info.homepage "")
                               (type info.homepage :userdata)) ; #<userdata NULL>
                           (string.format "https://github.com/%s" info.full_name)
                           info.homepage)
             :license (if (and info.license
                               (not= (type info.license) :userdata)) ; #<userdata NULL>
                          {:key info.license.key
                           :name info.license.name}
                          nil)}]
    (setmetatable obj github.repo)))

(github.repo.new "mnacamura/iron.nvim")

(fn github.repo.get_recent_commit [self branch]
  (let [branch (or branch self.default_branch)
        body (github.get (string.format "repos/%s/commits/%s" self.full_name branch))]
    {: branch
     :date body.commit.committer.date
     :sha body.sha}))


(local plugin {})

(set plugin.__index plugin)

(fn plugin.new [spec]
  (match (type spec)
    :string (let [repo (github.repo.new spec)
                  recent_commit (repo:get_recent_commit)
                  attr_name (string.gsub repo.name "%." "-")
                  obj {: attr_name 
                       :pname attr_name
                       :version (string.match recent_commit.date "^%d%d%d%d%-%d%d%-%d%d")
                       :full_name repo.full_name
                       :owner repo.owner
                       :name repo.name
                       :branch recent_commit.branch
                       :rev recent_commit.sha
                       :url (string.format "https://github.com/%s/archive/%s.tar.gz"
                                           repo.full_name
                                           recent_commit.sha)
                       :description repo.description
                       :homepage repo.homepage
                       :license repo.license}]
              (set obj.sha256 (nix.prefetch-url obj.url))
              (setmetatable obj plugin))
    _ (error "plugin spec undefined")))

(fn plugin.to_nixexpr [self]
  (string.format
    "  %s = buildVimPlugin {
    pname = \"%s\";
    version = \"%s\";
    src = fetchurl {
      url = \"%s\";
      sha256 = \"%s\";
    };
    meta = with lib; {
      description = \"%s\";
      homepage = \"%s\";%s
    };
  };
"
    self.attr_name
    self.pname
    self.version
    self.url
    self.sha256
    self.description
    self.homepage
    (let [license (and self.license
                       (nix.license_from_github_license_key self.license.key))]
      (if license
          (string.format "\n      license = with licenses; [ %s ];" license)
          ""))))

(fn plugin.to_markdown_bullet [self]
  (string.format
    "- [%s](https://github.com/%s) (branch: %s, recent commit: %s, package name: `%s`)\n"
    self.full_name
    self.full_name
    self.branch
    self.version
    self.attr_name))

(fn plugin.to_markdown_table_row [self]
  (string.format
    "| [%s](https://github.com/%s) | %s | %s | `%s` |\n"
    self.full_name
    self.full_name
    self.branch
    self.version
    self.attr_name))


(local specs (cjson.decode (slurp "manifest.json")))


(fn main []
  (local program_name (basename (. arg 0)))
  (with-open [nix (io.open "pkgs/vim-plugins.nix" :w)
              readme (io.open "README.md" :w)]
    (nix:write "{ lib, buildVimPlugin, fetchurl }:\n\n{\n")
    (readme:write (slurp "assets/README_header.md"))
    (each [_ spec (ipairs specs)]
      (io.stderr:write (string.format "%s: update %s..." program_name spec))
      (let [pkg (plugin.new spec)]
        (nix:write (pkg:to_nixexpr))
        (readme:write (pkg:to_markdown_table_row)))
      (io.stderr:write "done\n"))
    (nix:write "}\n")
    (readme:write (slurp "assets/README_footer.md"))))

(main)
